const int c_AbilityTypeNone = 0;
const int c_AbilityTypeYamatoGun = 1;
const int c_AbilityTypePsiStorm = 2;
const int c_AbilityTypePurificationNovaTargeted = 3;
const int c_AbilityTypeRavagerCorrosiveBile = 4;
const int c_AbilityTypeParasiticBomb = 5;
const int c_AbilityTypeAlarakDeadlyCharge = 6;
const int c_AbilityTypeShrineGuardianAntiGrav = 7;
const int c_AbilityTypeArtanisResurgence = 8;
const int c_AbilityTypeAlarakKnockback = 9;
const int c_AbilityTypeCount = 10;

const int c_AbilityKindActive = 0;
const int c_AbilityKindPassive = 1;

const int c_AbilityRangeWeapon = 0;
const int c_AbilityRangeFixed = 1;

bool AbilityCastAction(unit inCaster, unit inTaget, string type);

struct AbilityData {
    int index;
    string type;
    string cooldown;
    string name;
    int kind;
    int rangeType;
    funcref<AbilityCastAction> castAction;
};
typedef structref<AbilityData> AbilityDataRef;

static AbilityData[c_AbilityTypeCount] gv_abilitydata_types;
static int gv_abilitydata_dataByType;

void AbilityDataCopy(AbilityDataRef dest, AbilityDataRef source);
bool AbilityDataGet(int inIndex, AbilityDataRef outAbility);
string AbilityDataGetType(int inIndex);
string AbilityDataGetName(int inIndex);
bool AbilityDataGetByType(string inType, AbilityDataRef outAbility);
fixed AbilityDataGetCoolDown(unit inUnit, AbilityDataRef inAbility);
bool AbilityDataApply(unit inUnit, string inType);
bool AbilityDataCast(unit inCaster, unit inTarget, AbilityDataRef inAbility);
void InitAbilityData();

void AbilityDataCopy(AbilityDataRef dest, AbilityDataRef source) {
    dest.index = source.index;
    dest.type = source.type;
    dest.cooldown = source.cooldown;
    dest.kind = source.kind;
    dest.rangeType = source.rangeType;
    dest.name = source.name;
    dest.castAction = source.castAction;
}

bool AbilityDataGet(int inIndex, AbilityDataRef outAbility) {
    if (inIndex < 0 || inIndex >= c_AbilityTypeCount) {
        return false;
    }
    AbilityDataCopy(outAbility, gv_abilitydata_types[inIndex]);
    return true;
}

string AbilityDataGetType(int inIndex) {
    if (inIndex < 0 || inIndex >= c_AbilityTypeCount) {
        return "";
    }
    return gv_abilitydata_types[inIndex].type;
}

string AbilityDataGetName(int inIndex) {
    if (inIndex < 0 || inIndex >= c_AbilityTypeCount) {
        return "";
    }
    return gv_abilitydata_types[inIndex].name;
}

bool AbilityDataGetByType(string inType, AbilityDataRef outAbility) {
    if (!DataTableInstanceValueExists(gv_abilitydata_dataByType, inType)) {
        return false;
    }
    AbilityDataCopy(outAbility, gv_abilitydata_types[DataTableInstanceGetInt(gv_abilitydata_dataByType, inType)]);
    return true;
}

fixed AbilityDataGetCoolDown(unit inUnit, AbilityDataRef inAbility) {
    if (inAbility.cooldown == "None") {
        return 6000;
    }
    return UnitAbilityGetCooldown(inUnit, inAbility.type, inAbility.cooldown);
}

bool AbilityDataApply(unit inUnit, string inType) {
    AbilityData lv_ability;
    UnitAbilityRemove(inUnit, UnitAbilityGet(inUnit, 5));
    if (inType != "None") {
        if (AbilityDataGetByType(inType, lv_ability) && lv_ability.kind == c_AbilityKindPassive) {
            UnitBehaviorAdd(inUnit, inType, inUnit, 1);
        } else {
            UnitAbilityAdd(inUnit, inType);
        }
    }
    return true;
}

bool AbilityDataCast(unit inCaster, unit inTarget, AbilityDataRef inAbility) {
    int lv_index;
    fixed lv_range;

    if (!UnitIsAlive(inCaster) || !UnitIsAlive(inTarget)) {
        return false;
    }
    if (inAbility.rangeType == c_AbilityRangeWeapon) {
        for (lv_index = 1; lv_index <= UnitWeaponCount(inCaster); lv_index += 1) {
            if (UnitWeaponIsEnabled(inCaster, lv_index) && UnitWeaponRange(inCaster, lv_index) > lv_range) {
                lv_range = UnitWeaponRange(inCaster, lv_index);
            }
        }
        if ((lv_range + UnitGetPropertyFixed(inCaster, c_unitPropRadius, true)) * 1.2 < DistanceBetweenPoints(UnitGetPosition(inCaster), UnitGetPosition(inTarget))) {
            return false;
        }
    } else if (inAbility.rangeType == c_AbilityRangeFixed) {
        lv_range = UnitAbilityEffectInstantGetRange(inCaster, inAbility.type);
        if (lv_range < DistanceBetweenPoints(UnitGetPosition(inCaster), UnitGetPosition(inTarget))) {
            return false;
        }
    }
    if (inAbility.castAction == null) {
        return true;
    }
    return inAbility.castAction(inCaster, inTarget, inAbility.type);
}

static bool abilitydata_CastTargetUnit(unit inCaster, unit inTaget, string type) {
    order lv_order = OrderTargetingUnit(AbilityCommand(type, 0), inTaget);
    return UnitIssueOrder(inCaster, lv_order, c_orderQueueReplace);
}

static bool abilitydata_CastTargetPoint(unit inCaster, unit inTaget, string type) {
    order lv_order = OrderTargetingPoint(AbilityCommand(type, 0), UnitGetPosition(inTaget));
    return UnitIssueOrder(inCaster, lv_order, c_orderQueueReplace);
}

void InitAbilityData() {
    AbilityDataRef lv_ability;
    int lv_index;

    DataTableInstanceCreate();
    gv_abilitydata_dataByType = DataTableInstanceLastCreated();

    lv_ability = gv_abilitydata_types[c_AbilityTypeNone];
    lv_ability.index = c_AbilityTypeNone;
    lv_ability.type = "None";
    lv_ability.cooldown = "None";
    lv_ability.name = "无";
    lv_ability.castAction = null;

    lv_ability = gv_abilitydata_types[c_AbilityTypeYamatoGun];
    lv_ability.index = c_AbilityTypeYamatoGun;
    lv_ability.type = "Yamato";
    lv_ability.cooldown = "Yamato";
    lv_ability.name = "大和炮";
    lv_ability.castAction = abilitydata_CastTargetUnit;

    lv_ability = gv_abilitydata_types[c_AbilityTypePsiStorm];
    lv_ability.index = c_AbilityTypePsiStorm;
    lv_ability.type = "PsiStorm";
    lv_ability.cooldown = "Abil/PsiStorm";
    lv_ability.name = "灵能风暴";
    lv_ability.castAction = abilitydata_CastTargetPoint;

    lv_ability = gv_abilitydata_types[c_AbilityTypePurificationNovaTargeted];
    lv_ability.index = c_AbilityTypePurificationNovaTargeted;
    lv_ability.type = "PurificationNovaTargeted";
    lv_ability.cooldown = "Abil/PurificationNovaTargeted";
    lv_ability.name = "净化新星";
    lv_ability.castAction = abilitydata_CastTargetPoint;

    lv_ability = gv_abilitydata_types[c_AbilityTypeRavagerCorrosiveBile];
    lv_ability.index = c_AbilityTypeRavagerCorrosiveBile;
    lv_ability.type = "RavagerCorrosiveBile";
    lv_ability.cooldown = "Abil/RavagerCorrosiveBile";
    lv_ability.name = "腐蚀胆汁";
    lv_ability.castAction = abilitydata_CastTargetPoint;

    lv_ability = gv_abilitydata_types[c_AbilityTypeParasiticBomb];
    lv_ability.index = c_AbilityTypeParasiticBomb;
    lv_ability.type = "ParasiticBomb";
    lv_ability.cooldown = "Abil/ParasiticBomb";
    lv_ability.name = "寄生弹";
    lv_ability.castAction = abilitydata_CastTargetUnit;

    lv_ability = gv_abilitydata_types[c_AbilityTypeAlarakDeadlyCharge];
    lv_ability.index = c_AbilityTypeAlarakDeadlyCharge;
    lv_ability.type = "AlarakDeadlyCharge";
    lv_ability.cooldown = "Charge";
    lv_ability.name = "致命冲锋";
    lv_ability.castAction = null;

    lv_ability = gv_abilitydata_types[c_AbilityTypeShrineGuardianAntiGrav];
    lv_ability.index = c_AbilityTypeShrineGuardianAntiGrav;
    lv_ability.type = "ShrineGuardianAntiGrav";
    lv_ability.cooldown = "Abil/ShrineGuardianAntiGrav";
    lv_ability.name = "反重力";
    lv_ability.castAction = abilitydata_CastTargetUnit;

    lv_ability = gv_abilitydata_types[c_AbilityTypeArtanisResurgence];
    lv_ability.index = c_AbilityTypeArtanisResurgence;
    lv_ability.type = "ArtanisResurgence";
    lv_ability.cooldown = "None";
    lv_ability.kind = c_AbilityKindPassive;
    lv_ability.name = "坦克炸弹";
    lv_ability.castAction = null;

    lv_ability = gv_abilitydata_types[c_AbilityTypeAlarakKnockback];
    lv_ability.index = c_AbilityTypeAlarakKnockback;
    lv_ability.type = "AlarakKnockback";
    lv_ability.cooldown = "Abil/AlarakKnockback";
    lv_ability.rangeType = c_AbilityRangeFixed;
    lv_ability.name = "湮灭波";
    lv_ability.castAction = abilitydata_CastTargetPoint;

    for (lv_index = 0; lv_index < c_AbilityTypeCount; lv_index += 1) {
        lv_ability = gv_abilitydata_types[lv_index];
        if (lv_ability.name != null) {
            DataTableInstanceSetInt(gv_abilitydata_dataByType, lv_ability.type, lv_index);
        }
    }
}