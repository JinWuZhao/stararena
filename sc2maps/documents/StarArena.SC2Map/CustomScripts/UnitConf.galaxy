// Variable Declarations
static int gv_unitdata_dataTypeOfUnitType;

const int c_UnitDataTypeAliasCount = 5;
struct UnitData {
    int dataType;
    string unitType;
    string[c_UnitDataTypeAliasCount] typeAlias;
    int costPoints;
    int rewardPoints;
    string servantType;
};
typedef structref<UnitData> UnitDataRef;

const int c_UnitDataTypesMengskHellion = 0;
const int c_UnitDataTypesSiegeTank = 1;
const int c_UnitDataTypesThorAP = 2;
const int c_UnitDataTypesBattlecruiser = 3;
const int c_UnitDataTypesCount = 4;

typedef UnitData[c_UnitDataTypesCount] UnitDataArray;
typedef arrayref<UnitDataArray> UnitDataArrayRef;

static UnitDataArray gv_unitdata_unitTypes;

// Function Declarations
void UnitDataGet(int unitDataType, UnitDataRef outData);
bool UnitDataGetByUnitType(string unitType, UnitDataRef outData);
void InitUnitData();

static void unitdata_CopyUnitData(UnitDataRef dest, UnitDataRef source);

// Functions
void UnitDataGet(int unitDataType, UnitDataRef outData) {
    unitdata_CopyUnitData(outData, gv_unitdata_unitTypes[unitDataType]);
}

bool UnitDataGetByUnitType(string unitType, UnitDataRef outData) {
    if (arena_gf_StringIsEmpty(unitType)) {
        return false;
    }
    if (!DataTableInstanceValueExists(gv_unitdata_dataTypeOfUnitType, unitType)) {
        return false;
    }
    unitdata_CopyUnitData(outData, gv_unitdata_unitTypes[DataTableInstanceGetInt(gv_unitdata_dataTypeOfUnitType, unitType)]);
    return true;
}

void InitUnitData() {
    int lv_index;
    int k;
    UnitData emptyData;
    UnitData data;

    data.dataType = c_UnitDataTypesMengskHellion;
    data.unitType = "MengskHellion";
    data.costPoints = 0;
    data.rewardPoints = 100;
    data.servantType = "MengskHellion";
    unitdata_CopyUnitData(gv_unitdata_unitTypes[c_UnitDataTypesMengskHellion], data);

    unitdata_CopyUnitData(data, emptyData);
    data.dataType = c_UnitDataTypesSiegeTank;
    data.unitType = "SiegeTank";
    data.typeAlias[0] = "SiegeTankSieged";
    data.costPoints = 100;
    data.rewardPoints = 150;
    data.servantType = "SiegeTank";
    unitdata_CopyUnitData(gv_unitdata_unitTypes[c_UnitDataTypesSiegeTank], data);

    unitdata_CopyUnitData(data, emptyData);
    data.dataType = c_UnitDataTypesThorAP;
    data.unitType = "ThorAP";
    data.typeAlias[0] = "Thor";
    data.costPoints = 300;
    data.rewardPoints = 200;
    data.servantType = "ThorAP";
    unitdata_CopyUnitData(gv_unitdata_unitTypes[c_UnitDataTypesThorAP], data);

    unitdata_CopyUnitData(data, emptyData);
    data.dataType = c_UnitDataTypesBattlecruiser;
    data.unitType = "Battlecruiser";
    data.costPoints = 500;
    data.rewardPoints = 300;
    data.servantType = "Battlecruiser";
    unitdata_CopyUnitData(gv_unitdata_unitTypes[c_UnitDataTypesBattlecruiser], data);

    DataTableInstanceCreate();
    gv_unitdata_dataTypeOfUnitType = DataTableInstanceLastCreated();
    for (lv_index = 0; lv_index < c_UnitDataTypesCount; lv_index += 1) {
        DataTableInstanceSetInt(gv_unitdata_dataTypeOfUnitType, gv_unitdata_unitTypes[lv_index].unitType, lv_index);

        for (k = 0; k < c_UnitDataTypeAliasCount; k += 1) {
            if (!arena_gf_StringIsEmpty(gv_unitdata_unitTypes[lv_index].typeAlias[k])) {
                DataTableInstanceSetInt(gv_unitdata_dataTypeOfUnitType, gv_unitdata_unitTypes[lv_index].typeAlias[k], lv_index);
            }
        }
    }
}

static void unitdata_CopyUnitData(UnitDataRef dest, UnitDataRef source) {
    int lv_index;

    dest.dataType = source.dataType;
    dest.unitType = source.unitType;
    dest.costPoints = source.costPoints;
    dest.rewardPoints = source.rewardPoints;
    dest.servantType = source.servantType;

    for (lv_index = 0; lv_index < c_UnitDataTypeAliasCount; lv_index += 1) {
        dest.typeAlias[lv_index] = source.typeAlias[lv_index];
    }
}